import static org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL

plugins {
    id "nebula.optional-base" version "3.2.0"
    id "com.palantir.git-version" version "0.8.0"
    id "org.sonarqube" version "2.5"
}

ext {
    feignVersion = "9.5.1"
    retrofitVersion = "2.3.0"
    okHttpVersion = "3.9.0"
    slf4jVersion="1.7.25"
}

apply plugin: "java"
apply plugin: "jacoco"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-scalars:${retrofitVersion}"
//    compile "com.squareup.retrofit2:converter-simplexml:${retrofitVersion}"

    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    compile "com.google.code.findbugs:jsr305:3.0.2", optional
    compile "org.projectlombok:lombok:1.16.18", optional
    compile "javax.validation:validation-api:2.0.0.Final", optional
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.0"
    compile "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.9.0"

    testRuntime "org.codehaus.janino:janino:3.0.7"

    testCompile "junit:junit:4.12"
    testCompile "ch.qos.logback:logback-classic:1.2.3"
    testCompile "io.github.benas:random-beans:3.7.0"
    testCompile "org.assertj:assertj-core:3.8.0"
    testCompile "org.mockito:mockito-core:2.11.0"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionType = ALL
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

jacocoTestReport {
    group = "report"
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }

    outputs.upToDateWhen { false }

    systemProperties System.properties
    systemProperties 'logback.configurationFile' : new File(projectDir,'src/test/resources/logback.xml').absolutePath

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}